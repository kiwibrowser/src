// Copyright 2018 The Chromium Authors. All rights reserved.
// Use of this source code is governed by a BSD-style license that can be
// found in the LICENSE file.

#import "ios/chrome/browser/ui/table_view/cells/table_view_text_header_footer_item.h"

#include "base/mac/foundation_util.h"
#import "ios/chrome/browser/ui/table_view/chrome_table_view_styler.h"
#include "testing/gtest/include/gtest/gtest.h"
#include "testing/gtest_mac.h"
#include "testing/platform_test.h"

#if !defined(__has_feature) || !__has_feature(objc_arc)
#error "This file requires ARC support."
#endif

namespace {
using TableViewTextHeaderFooterItemTest = PlatformTest;
}

// Tests that the UILabels are set properly after a call to
// |configureHeaderFooterView:|.
TEST_F(TableViewTextHeaderFooterItemTest, HeaderFooterTextLabels) {
  NSString* text = @"HeaderFooter text";

  TableViewTextHeaderFooterItem* item =
      [[TableViewTextHeaderFooterItem alloc] initWithType:0];
  item.text = text;

  id headerFooter = [[[item cellClass] alloc] init];
  ASSERT_TRUE(
      [headerFooter isMemberOfClass:[TableViewTextHeaderFooterView class]]);

  TableViewTextHeaderFooterView* textHeaderFooter =
      base::mac::ObjCCastStrict<TableViewTextHeaderFooterView>(headerFooter);
  EXPECT_FALSE(textHeaderFooter.textLabel.text);

  ChromeTableViewStyler* styler = [[ChromeTableViewStyler alloc] init];
  [item configureHeaderFooterView:textHeaderFooter withStyler:styler];
  EXPECT_NSEQ(text, textHeaderFooter.textLabel.text);
}

TEST_F(TableViewTextHeaderFooterItemTest, Styler) {
  TableViewTextHeaderFooterItem* item =
      [[TableViewTextHeaderFooterItem alloc] initWithType:0];
  id headerFooter = [[[item cellClass] alloc] init];
  ASSERT_TRUE(
      [headerFooter isMemberOfClass:[TableViewTextHeaderFooterView class]]);
  TableViewTextHeaderFooterView* textHeaderFooter =
      base::mac::ObjCCastStrict<TableViewTextHeaderFooterView>(headerFooter);

  ChromeTableViewStyler* styler = [[ChromeTableViewStyler alloc] init];
  UIColor* testColor = [UIColor redColor];
  styler.tableViewBackgroundColor = testColor;
  [item configureHeaderFooterView:textHeaderFooter withStyler:styler];
  EXPECT_NSEQ(testColor, textHeaderFooter.contentView.backgroundColor);
}
